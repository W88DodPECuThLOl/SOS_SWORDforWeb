;
; Z80 CTC TEST
;
; ・CTCの割り込みを使い、グラフィック画面を描画する
; ・もう一度実行すると、CTCをリセットし動作を停止する
;

	org $8000

MPRINT	EQU	$1FE2
PRTHL   EQU $1FBE
LTNL    EQU $1FEE

CTC_ADDR EQU	$1FA0 ; CTCのIOアドレス

	CALL MPRINT
	DB "Z80 CTC TEST"
	DB 13, 0

	DI ; 割り込み無効

	; CTCを探す
	CALL SEARCHCTC
	LD HL,(CTC)
	LD A,L
	OR H
	JR NZ, CTC_FND
	; 見つからなかった
	CALL MPRINT
	DB "CTC NOT FOUND", 13, 0
	RET
CTC_FND:
	; 見つかった
	; CTCのポートアドレスを表示
	CALL MPRINT
	DB "CTC FOUND! PORT $", 0
	LD HL,(CTC)
	CALL PRTHL
	CALL LTNL

	; CTCリセット
	CALL CTC_RESET

	; 呼び出すたびに、実行、停止させる
	LD A,(CNT)
	XOR 1
	LD (CNT),A
	JR Z,EXIT

	; 呼び出される割り込み処理
	LD HL,INT
	LD ($005E),HL

	; CTC Ch.0設定
	LD BC,(CTC)
	LD A,$07   ; 割り込み無効、タイマモード、16分周
	OUT (C),A
	LD A,125   ; 125*16/4MHzで、ZC/TR出力（CTC Ch.3のカウンタが１つ加算される）
	OUT (C),A
	LD A,$58   ; 割り込みベクタ
	OUT (C),A
	; CTC Ch.3設定
	INC C
	INC C
	INC C
	LD A,$C7   ; 割り込み有効、カウンタモード
	OUT (C),A
	LD A,64    ; 割り込みの間隔 64回で割り込み発生
	OUT (C),A
EXIT:
	EI ; 割り込み有効
	RET

;
; CTC リセット
;
CTC_RESET:
	LD BC,(CTC)
	LD A,3
	OUT (C),A
	INC C
	OUT (C),A
	INC C
	OUT (C),A
	INC C
	OUT (C),A
	RET

;
; CTCを見つける
;
SEARCHCTC:
	LD BC,0
	LD (CTC),BC
	LD BC,00A04H
	CALL   CHKCTC
	LD BC,00704H
	CALL   CHKCTC
	LD BC,01FA8H
	CALL   CHKCTC
	LD BC,01FA0H
	;CALL   CHKCTC
	;RET
CHKCTC:
	PUSH   BC
	LD DE,04703H
INICTC1:
	INC    C
	OUT    (C),D
	DB 0EDH,071H   ;OUT (C),0  Z80未定義命令
	DEC    E
	JR NZ,INICTC1
	POP    BC
 
	LD DE,007FAH
	OUT    (C),D
	OUT    (C),E
	IN A,(C)
	CP E
	RET    NZ
	OUT    (C),D
	OUT    (C),D
	IN A,(C)
	CP D
	RET    NZ
	LD (CTC),BC
	RET

;
; CTCの割り込みで呼び出される
;
INT:
	PUSH AF
	PUSH BC
	LD BC,(GADD)
	LD A,B
	CP $40
	JR NC,OKOUT
	LD BC,$4000
	LD A,(GPAT)
	XOR $CC
	LD (GPAT),A
OKOUT:
	LD A,(GPAT)
	OUT (C),A
	INC BC
	LD (GADD),BC
	POP BC
	POP AF
	EI
	RETI
;
; ワーク
;
CTC:
	DW $0000
CNT:
	DB $00
GADD:
	DW $4000
GPAT:
	DB $CC